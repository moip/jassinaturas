apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group 'br.com.moip'
version '1.1.7'
archivesBaseName = "jassinaturas"
sourceCompatibility = 1.7

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'net.sourceforge.cobertura', name: 'cobertura', version: '2.0.3'
    testCompile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.25'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign', version: '1.4.3.RELEASE'
    compile group: 'io.github.openfeign', name: 'feign-gson', version: '9.3.1'
    compile group: 'io.github.openfeign', name: 'feign-slf4j', version: '9.3.1'
    
    
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    required {
        gradle.taskGraph.hasTask("uploadArchives")
    }
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'jassinaturas'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'JAssinaturas'
                url 'https://github.com/moip/jassinaturas'

                scm {
                    connection 'scm:git:ssh://git@github.com:moip/jassinaturas.git'
                    developerConnection 'scm:git:ssh://git@github.com:moip/jassinaturas.git'
                    url 'https://github.com/moip/jassinaturas'
                }

                licenses {
                    license {
                        name 'The MIT License (MIT)'
                        url 'http://opensource.org/licenses/MIT'
                    }
                }

                developers {
                    developer {
                        id 'ecolabardini'
                        name 'Eduardo Colabardini'
                        email 'eduardocolabardini@gmail.com'
                    }
                }

            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}
